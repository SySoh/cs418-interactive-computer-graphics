<!DOCTYPE HTML>
<html lang="en">
<head>
    <title>Teapot skybox - CS418 MP3</title>
    <meta charset="utf-8">
</head>

<script id="shader-vs-skybox" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;

        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        varying vec3 vertPos;

        void main(void) {
            vertPos = aVertexPosition;
            gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        }
</script>

<script id="shader-fs-skybox" type="x-shader/x-fragment">
    precision mediump float;

    varying vec3 vertPos;

    uniform samplerCube uCubeSampler;

    void main(void) {
        gl_FragColor = textureCube(uCubeSampler, vertPos);
    }
</script>



<script id="shader-vs-teapot" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;

        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        varying vec3 normalInterp;
        varying vec3 vertPos;

        void main(void) {
            gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
            vertPos = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;
            normalInterp = normalize(aVertexNormal);
        }
</script>


<script id="shader-fs-teapot" type="x-shader/x-fragment">
    precision mediump float;

    uniform mat3 uNMatrix;
    varying vec3 vertPos;
    varying vec3 normalInterp;
    uniform mat3 uInverseViewTransform;

    uniform vec3 uLightPosition; // Fixed light source location
    uniform vec3 uAmbientLightColor;
    uniform vec3 uSpecularLightColor;
    uniform vec3 uDiffuseLightColor;

    uniform samplerCube uSkyboxSampler;

    const float shininess = 5.0;

    void main(void) {

        // Calculate the vector (l) to the light source
        vec3 vectorToLightSource = normalize(uLightPosition - vertPos);

        // Transform the normal (n) to eye coordinates
        vec3 normalEye = normalize(uNMatrix * normalInterp);

        // Calculate n dot l for diffuse lighting
        float diffuseLightWeighting = max(dot(normalEye,
                                    vectorToLightSource), 0.0);

        // Calculate the reflection vector (r) that is needed for specular light
        vec3 reflectionVector = normalize(reflect(-vectorToLightSource,normalEye));

        vec3 eyeDirection = normalize(-vertPos);

        float rdotv = max(dot(reflectionVector, eyeDirection), 0.0);

        float specularLightWeighting = pow(rdotv, shininess);

        vec3 l = uInverseViewTransform * reflect(eyeDirection, normalEye);

        // get texture color from skybox
        vec4 vSkyboxColor = textureCube(uSkyboxSampler, -l);

        // Sum up all three reflection components
        vec3 vColor = (uAmbientLightColor * vSkyboxColor.xyz)
            + (uSpecularLightColor * vSkyboxColor.xyz) * specularLightWeighting
            + (uDiffuseLightColor * vSkyboxColor.xyz) * diffuseLightWeighting;

        gl_FragColor = vec4(vColor, 1.0);
    }
</script>


<script src="gl-matrix-min.js"></script>
<script src="webgl-utils.js"></script>
<script src="mp3.js"></script>
<script src="jquery-3.1.1.min.js"></script>
<body id="body" onload="startup();">
<canvas id="myGLCanvas" width="1000" height="1000"></canvas>
</body>
</html>
